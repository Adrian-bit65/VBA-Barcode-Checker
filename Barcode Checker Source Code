' This is a function used to compare barcodes for batches that are scanned in on the Hamilton with a list pasted in from the batch list on Epic.
' First, paste in the barcodes from the Excel file that the Hamilton makes on the green box in column C
' Second, paste in the list from the snapshot of the batch (clicking and dragging from "1." to the end of "COVID.DL") on the green box in column D
' Lastly, go to the Developer tab and hit "Macros" on the left-hand side, select "barcodeListandCheck", then hit Run

Sub barcodeListandCheck()
Dim x As Integer
Dim N As Integer
N = 3
' In this For Loop, we are looking through the 92 potential spots from the batch list and pulling the Accession numbers using the criteria that they are above 100
' Numbers that are above 100, which are often the Accession numbers we are trying to compare, are pulled from column D and put in column G.
    For x = 2 To 93
    Set ahsNumbeR = Range("G" & x)
    Set numberPicker = Range("D" & N)
        If numberPicker > 100 Then
            ahsNumbeR.Value = numberPicker
        End If
    N = N + 5
    Next x
    
    Dim Str1 As String
    Str1 = "The following patient barcodes do not match: " ' Formatting String for MsgBox message
    Dim Y As Integer
    Y = 0 ' Counter for number of barcodes that don't match
    Dim Z As Integer
    Z = 0 ' Counter for total number of barcodes checked
    
    ' In this For Loop, we compare the barcodes pulled from the batch list (column G) with the barcodes scanned in from the Hamilton (column C)
    ' For every 92 potential spots, it will check if there are barcodes and if there are, it will then check if they match. A message is printed according to the outcome.
    For I = 2 To 93
    Set patientName = Range("A" & I)
    Set matchCheck = Range("B" & I)
    Set scannedBarcodes = Range("C" & I)
    Set pastedBarcodes = Range("G" & I)
    Dim J As Integer
    J = I - 1
        ' If blank, we will deem that as no patient present and grey it out
        If IsEmpty(scannedBarcodes) Then
            patientName.Value = "No Patient " & J
            matchCheck.Value = "No Barcode"
            Range("A" & I).Interior.Color = RGB(150, 153, 155) ' GMK 2B Standard Color Code
        ' If there's a match, we will colour cell green annd say it matches
        ElseIf scannedBarcodes = pastedBarcodes Then
            patientName.Value = "Patient " & J
            matchCheck.Value = "Barcodes match"
            Range("A" & I).Interior.Color = RGB(191, 227, 186) ' GMK Botanical Green (3A)
            Z = Z + 1
        ' If barcodes don't match, we colour cell red, say it doesn't match, and add the patient number to our output message
        ElseIf scannedBarcodes <> pastedBarcodes Then
            patientName.Value = "Patient " & J
            matchCheck.Value = "DOES NOT MATCH"
            Range("A" & I).Interior.Color = RGB(255, 40, 17) ' GMK RO2 Standard Color Code
            Dim Str2 As String
            Str2 = vbNewLine & vbNewLine & "Patient " & CStr(J) & "     " & CStr(Range("E" & (3 + (5 * (J - 1)))))
            Str1 = Str1 + Str2
            Y = Y + 1
            Z = Z + 1
        End If
    Next I
    If Y > 0 And Y < 25 Then ' Y increases with each mismatch. If there's more than one then we print the mismatches
        MsgBox Str1, vbCritical, "Check Completed"
    ElseIf Y >= 25 Then ' MsgBox limit is 25 and we rarely get even one mismatch
        MsgBox "Many barcodes don't match. Please check if formatting is correct.", vbExclamation, "Check Completed"
    ElseIf Y = 0 And Z > 0 Then ' If there's no mismatch (Y=0) and there are patients (Z>0)
        MsgBox "All barcodes match!", vbInformation, "Check Completed"
    ElseIf Z = 0 Then ' If there's no patients
        MsgBox "No patients detected. Please check if formatting is correct.", vbExclamation, "Check Completed"
    End If
    Dim answer As Integer
    answer = MsgBox("Would you like to reset table for next run?", vbYesNo + vbQuestion, "Reset?")
    If answer = vbYes Then
        Range("A2:G461").Clear 'Clear cells A2 to G461
        Range("C2:D2").Interior.Color = RGB(185, 206, 172) ' GMK Botanical Green
    Else
        'Leave it
    End If
End Sub
